(define (domain triangle-tire)

	(:requirements :typing :strips :probabilistic-effects)

	(:types location)

	(:predicates
		(spare-in ?loc - location)
		(vehicle-at ?loc - location)
		(road ?from - location ?to - location)
		(not-flattire)
		(hasspare))

	(:action load-tire
		:parameters (?loc - location)
		:precondition (and (vehicle-at ?loc) (spare-in ?loc))
		:effect (and (hasspare) (not (spare-in ?loc))))

	(:action move-car
		:parameters (?from - location ?to - location)
		:precondition (and (vehicle-at ?from) (road ?from ?to) (not-flattire))
		:effect (and (vehicle-at ?to) (not (vehicle-at ?from)) (probabilistic 0.5 (not (not-flattire)))))

	(:action changetire
		:parameters ()
		:precondition (hasspare)
		:effect (and (not (hasspare)) (not-flattire)))
)
